buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}


apply plugin: "maven"
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.12-2.0.5"
group = "betterwithmods"
archivesBaseName = "BetterWithMods"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

if (System.getenv().BUILD_NUMBER != null) {//adds the build number to the end of the version string if on a build server
    version += "-${System.getenv().BUILD_NUMBER}"
}

minecraft {
    version = "1.12-14.21.1.2426"
    runDir = "run"
    mappings = "snapshot_20170627"
    replace "%VERSION%": "${project.version}"
    useDepAts = true
}

repositories {
    maven { url "http://blamejared.com/maven" }
    maven { url 'http://dvs1.progwml6.com/files/maven' }
    maven { url "http://maven.amadornes.com/" }
    maven { url "http://www.ryanliptak.com/maven/" }
    mavenCentral()
}

dependencies {
    deobfCompile "mezz.jei:jei_1.12:4.7.2.77"
    deobfCompile "applecore:AppleCore:1.12-2.2.2:api"
    runtime "applecore:AppleCore:1.12-2.2.2:deobf"
}

jar {
    manifest {
        attributes 'FMLAT': 'bwm_at.cfg'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier "dev"
}

artifacts {
    archives deobfJar
}

configurations {
    deployerJars
}

dependencies {
    deployerJars 'org.apache.maven.wagon:wagon-ftp:2.2'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + System.getenv("local_maven"))
            pom {
                groupId = project.group
                version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'BetterWithMods'
                    url 'https://github.com/BeetoGuy/BetterWithMods'
                    scm {
                        url 'https://github.com/BeetoGuy/BetterWithMods.git'
                        connection 'scm:git:git@github.com:BeetoGuy/BetterWithMods.git'
                        developerConnection 'scm:git:git@github.com:BeetoGuy/BetterWithMods.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/BeetoGuy/BetterWithMods/issues'
                    }
                    developers {
                        developer {
                            id 'primetoxinz'
                            name 'primetoxinz'
                            roles { role 'developer' }
                        }
                        developer {
                            id 'beetoguy'
                            name 'beetoguy'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
